
{
  parserClass="com.jivesoftware.robot.intellij.plugin.parser.RobotParser"
  parserUtilClass="com.jivesoftware.robot.intellij.plugin.parser.RobotParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Robot"
  psiImplClassSuffix="Impl"
  psiPackage="com.jivesoftware.robot.intellij.plugin.psi"
  psiImplPackage="com.jivesoftware.robot.intellij.plugin.psi.impl"

  elementTypeHolderClass="com.jivesoftware.robot.intellij.plugin.parser.RobotTypes"
  elementTypeClass="com.jivesoftware.robot.intellij.plugin.parser.RobotElementType"
  tokenTypeClass="com.jivesoftware.robot.intellij.plugin.lexer.RobotToken"

  psiImplUtilClass="com.jivesoftware.robot.intellij.plugin.elements.RobotImplUtil"
  implements("KEYWORD")="com.jivesoftware.robot.intellij.plugin.elements.references.RobotNamedElement"
  implements("KEYWORD_DEF")="com.jivesoftware.robot.intellij.plugin.elements.references.IRobotKeywordDef"
  extends("KEYWORD_DEF")="com.jivesoftware.robot.intellij.plugin.elements.references.RobotKeywordDefBase"
  elementTypeClass("KEYWORD_DEF")="com.jivesoftware.robot.intellij.plugin.parser.RobotKeywordDefElementType"
  implements("TEST_CASE")="com.jivesoftware.robot.intellij.plugin.elements.references.RobotNamedElement"
}

robotFile ::= !<<eof>> (ROBOT_TABLE|EMPTY_LINE)* { methods=[getNameIdentifier getPresentation]}

ROBOT_TABLE ::= (SETTINGS_TABLE|VARIABLES_TABLE|TEST_CASES_TABLE|KEYWORDS_TABLE)

/* Settings Table Grammar */
SETTINGS_TABLE ::= SETTINGS_TABLE_HEADING SETTINGS_LINE*

SETTINGS_TABLE_HEADING ::= SETTINGS_TABLE_HEADING_TOKEN END_OF_LINE (NEWLINE_TOKEN | <<eof>>)

SETTINGS_LINE ::= COLUMN_SEP_TOKEN* SETTING END_OF_LINE (NEWLINE_TOKEN | <<eof>>) |
                  EMPTY_LINE |
                  ELLIPSES_LINE_SETTINGS

SETTING ::= FORCE_TAGS_SETTING | RESOURCE_SETTING | TEST_SETUP_SETTING | GENERIC_SETTING

TAG_LIST_OR_ELLIPSES_NO_COL ::= TAG_LIST | ELLIPSES_FOR_SETTINGS_TABLE
FORCE_TAGS_SETTING ::= FORCE_TAGS_SETTING_KEYWORD_TOKEN TAG_LIST_OR_ELLIPSES_NO_COL+ | FORCE_TAGS_SETTING_KEYWORD_TOKEN
SETTING_LIST ::= (COLUMN_SEP_TOKEN  KEYWORD_ARG)+
SETTING_LIST_OR_ELLIPSES ::= SETTING_LIST | ELLIPSES_FOR_SETTINGS_TABLE
GENERIC_SETTING ::= ROBOT_KEYWORD_TOKEN SETTING_LIST_OR_ELLIPSES+ | ROBOT_KEYWORD_TOKEN

TEST_SETUP_TOKEN ::=  (TEST_SETUP_SETTING_TOKEN | TEST_TEARDOWN_SETTING_TOKEN | SUITE_SETUP_SETTING_TOKEN)
TEST_SETUP_SETTING ::= TEST_SETUP_TOKEN ELLIPSES_FOR_SETTINGS_TABLE+ COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS |
                       TEST_SETUP_TOKEN COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS

RESOURCE_SETTING ::= RESOURCE_SETTING_TOKEN  COLUMN_SEP_TOKEN  KEYWORD_ARG


/* Variables table */
VARIABLES_TABLE ::= VARIABLES_TABLE_HEADING VARIABLES_LINE*

VARIABLES_TABLE_HEADING ::= VARIABLES_TABLE_HEADING_TOKEN END_OF_LINE (NEWLINE_TOKEN | <<eof>>)

VARIABLES_LINE ::= (ASSIGNMENT_TOKEN | VARIABLE_TOKEN)  (COLUMN_SEP_TOKEN   ASSIGNABLE)? END_OF_LINE (NEWLINE_TOKEN | <<eof>>) |
                   (ARRAY_ASSIGNMENT_TOKEN | ARRAY_VARIABLE_TOKEN)  (COLUMN_SEP_TOKEN   ARRAY_ASSIGNABLE)? END_OF_LINE  (NEWLINE_TOKEN | <<eof>>) |
                   EMPTY_LINE  |
                   ELLIPSES_LINE_SETTINGS

/* Test cases table */
TEST_CASES_TABLE ::= TEST_CASES_TABLE_HEADING EMPTY_LINE* TEST_CASE* { methods=[getNameIdentifier getPresentation]}

TEST_CASES_TABLE_HEADING ::= TEST_CASES_TABLE_HEADING_TOKEN END_OF_LINE (NEWLINE_TOKEN | <<eof>>)

TEST_CASE ::= TEST_CASE_HEADER TEST_CASE_SETTINGS TESTCASE_LINE+  { methods=[getName setName getNameIdentifier getPresentation]}

TEST_CASE_HEADER ::= TEST_CASE_HEADER_TOKEN END_OF_LINE (ELLIPSES_FOR_TEST_TABLE? NEWLINE_TOKEN | <<eof>>)

TEST_CASE_SETTINGS ::= TEST_SETTING_LINE*

TEST_SETTING_LINE ::= COLUMN_SEP_TOKEN  TEST_SETTING END_OF_LINE (NEWLINE_TOKEN | <<eof>>) | EMPTY_LINE

TEST_SETTING ::= (DOCUMENTATION_SETTING|TAGS_SETTING|SETUP_SETTING|TEARDOWN_SETTING|TEMPLATE_SETTING|TIMEOUT_SETTING)

DOCUMENTATION_SETTING ::= DOCUMENTATION_SETTING_TOKEN  (ELLIPSES_FOR_TEST_TABLE? COLUMN_SEP_TOKEN  DOCUMENTATION_TOKEN)*
TAG_LIST ::= (COLUMN_SEP_TOKEN TAG)+
TAG_LIST_OR_ELLIPSES ::= TAG_LIST | ELLIPSES_FOR_TEST_TABLE
TAGS_SETTING ::= TAGS_SETTING_TOKEN TAG_LIST_OR_ELLIPSES+ | TAGS_SETTING_TOKEN

SETUP_SETTING ::= SETUP_SETTING_TOKEN  COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS
TEARDOWN_SETTING ::= TEARDOWN_SETTING_TOKEN COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS
TEMPLATE_SETTING ::= TEMPLATE_SETTING_TOKEN COLUMN_SEP_TOKEN ROBOT_KEYWORD_TOKEN
TIMEOUT_SETTING ::= TIMEOUT_SETTING_TOKEN COLUMN_SEP_TOKEN TIMEOUT_VALUE_TOKEN

TESTCASE_LINE ::= COLUMN_SEP_TOKEN (KEYWORD_INVOCATION_TEST | VARIABLE_ASSIGN_TO_KEYWORD) END_OF_LINE (NEWLINE_TOKEN | <<eof>>) |
                  COLUMN_SEP_TOKEN ARGUMENT_LIST END_OF_LINE (NEWLINE_TOKEN | <<eof>>) |
                  EMPTY_LINE |
                  ELLIPSES_LINE_TEST |
                  FOR_LOOP_IN_RANGE  |
                  FOR_LOOP_IN

/* Keywords table */
KEYWORDS_TABLE ::= KEYWORDS_TABLE_HEADING (EMPTY_LINE | KEYWORD_DEFINITION)* { methods=[getNameIdentifier getPresentation]}

KEYWORDS_TABLE_HEADING ::= KEYWORDS_TABLE_HEADING_TOKEN END_OF_LINE (NEWLINE_TOKEN | <<eof>>)

KEYWORD_DEFINITION ::= KEYWORD_DEFINITION_HEADER KEYWORD_SETTINGS KEYWORD_LINE* RETURN_LINE?

KEYWORD_DEFINITION_HEADER ::= KEYWORD_DEF END_OF_LINE (NEWLINE_TOKEN | <<eof>>)

KEYWORD_SETTINGS ::= KEYWORD_SETTING_LINE*
KEYWORD_SETTING_LINE ::= COLUMN_SEP_TOKEN KEYWORD_SETTING END_OF_LINE (NEWLINE_TOKEN | <<eof>>) | EMPTY_LINE
KEYWORD_SETTING ::= ARGUMENTS_SETTING | DOCUMENTATION_SETTING | TEARDOWN_SETTING | TIMEOUT_SETTING

ARGUMENTS_SETTING ::= ARGUMENTS_SETTING_TOKEN (COLUMN_SEP_TOKEN ARGUMENT_DEF)*
ARGUMENT_DEF ::= VARIABLE_TOKEN | ARRAY_VARIABLE_TOKEN | ASSIGNMENT_TOKEN WHITESPACE_TOKEN? KEYWORD_ARG | ARRAY_ASSIGNMENT_TOKEN WHITESPACE_TOKEN? KEYWORD_ARG

KEYWORD_LINE ::= COLUMN_SEP_TOKEN ( KEYWORD_INVOCATION_TEST | VARIABLE_ASSIGN_TO_KEYWORD ) END_OF_LINE (NEWLINE_TOKEN | <<eof>>)  |
                 EMPTY_LINE |
                 ELLIPSES_LINE_TEST |
                 FOR_LOOP_IN_RANGE  |
                 FOR_LOOP_IN

INDENTED_KEYWORD_LINE ::= COLUMN_SEP_TOKEN  EMPTY_CELL_TOKEN COLUMN_SEP_TOKEN ( INDENTED_KEYWORD_INVOCATION_TEST | INDENTED_VARIABLE_ASSIGN_TO_KEYWORD ) END_OF_LINE (NEWLINE_TOKEN | <<eof>>)  |
                 EMPTY_LINE |
                 INDENTED_ELLIPSES_LINE_TEST

RETURN_LINE ::= COLUMN_SEP_TOKEN RETURN_SETTING END_OF_LINE (NEWLINE_TOKEN | <<eof>>)
RETURN_SETTING ::= RETURN_SETTING_TOKEN (COLUMN_SEP_TOKEN KEYWORD_ARG)+


/* Shared grammar */
SINGLE_VARIABLE ::= VARIABLE_TOKEN
SINGLE_VARIABLE_LIST ::= SINGLE_VARIABLE (COLUMN_SEP_TOKEN SINGLE_VARIABLE)*
VARIABLE ::= VARIABLE_TOKEN | ARRAY_VARIABLE_TOKEN
ARGUMENT_LIST ::= KEYWORD_ARG (COLUMN_SEP_TOKEN KEYWORD_ARG)*
ASSIGNMENT ::= ASSIGNMENT_TOKEN | ARRAY_ASSIGNMENT_TOKEN
KEYWORD_ARG_LIST ::= (COLUMN_SEP_TOKEN KEYWORD_ARG)+
KEYWORD_INVOCATION_TEST ::= KEYWORD (KEYWORD_ARG_LIST | ELLIPSES_FOR_TEST_TABLE)+ | KEYWORD
INDENTED_KEYWORD_INVOCATION_TEST ::= KEYWORD (KEYWORD_ARG_LIST | INDENTED_ELLIPSES_FOR_TEST_TABLE)+ | KEYWORD
KEYWORD_INVOCATION_SETTINGS ::= KEYWORD (KEYWORD_ARG | ELLIPSES_FOR_SETTINGS_TABLE)+ | KEYWORD
ASSIGNABLE ::= KEYWORD_INVOCATION_SETTINGS | KEYWORD_ARG
ARRAY_ASSIGNABLE ::= KEYWORD_INVOCATION_SETTINGS | KEYWORD_ARG ( ELLIPSES_FOR_SETTINGS_TABLE? COLUMN_SEP_TOKEN KEYWORD_ARG)*
VARIABLE_ASSIGN_TO_KEYWORD ::= ( MULTI_ASSIGNMENT | SINGLE_ASSIGNMENT ) ELLIPSES_FOR_TEST_TABLE* COLUMN_SEP_TOKEN  KEYWORD_INVOCATION_TEST
INDENTED_VARIABLE_ASSIGN_TO_KEYWORD ::= ( MULTI_ASSIGNMENT | SINGLE_ASSIGNMENT ) INDENTED_ELLIPSES_FOR_TEST_TABLE* COLUMN_SEP_TOKEN  INDENTED_KEYWORD_INVOCATION_TEST
SINGLE_ASSIGNMENT ::= VARIABLE | ASSIGNMENT
MULTI_ASSIGNMENT ::=  VARIABLE (COLUMN_SEP_TOKEN  VARIABLE)+
                    | VARIABLE (COLUMN_SEP_TOKEN VARIABLE)* COLUMN_SEP_TOKEN ASSIGNMENT
KEYWORD_ARG ::= VARIABLE | ROBOT_KEYWORD_ARG_TOKEN | EMPTY_CELL_TOKEN | INTEGER_TOKEN
TAG ::= TAG_TOKEN
END_OF_LINE ::= WHITESPACE_TOKEN? COLUMN_SEP_TOKEN? COMMENT_TOKEN?
ELLIPSES_LINE_TEST ::= COLUMN_SEP_TOKEN  ELLIPSES  END_OF_LINE
INDENTED_ELLIPSES_LINE_TEST ::= COLUMN_SEP_TOKEN EMPTY_CELL_TOKEN COLUMN_SEP_TOKEN  ELLIPSES  END_OF_LINE
ELLIPSES_LINE_SETTINGS ::= COLUMN_SEP_TOKEN? WHITESPACE_TOKEN? ELLIPSES END_OF_LINE
EMPTY_LINE  ::= !<<eof>> END_OF_LINE (NEWLINE_TOKEN | <<eof>>)
ELLIPSES ::= ELLIPSES_TOKEN
ELLIPSES_FOR_TEST_TABLE    ::= END_OF_LINE NEWLINE_TOKEN COLUMN_SEP_TOKEN ELLIPSES
INDENTED_ELLIPSES_FOR_TEST_TABLE  ::= END_OF_LINE NEWLINE_TOKEN COLUMN_SEP_TOKEN EMPTY_CELL_TOKEN COLUMN_SEP_TOKEN ELLIPSES
ELLIPSES_FOR_SETTINGS_TABLE ::= END_OF_LINE NEWLINE_TOKEN WHITESPACE_TOKEN? COLUMN_SEP_TOKEN? ELLIPSES

FOR_VAR_IN_RANGE ::= FOR_LOOP_START_TOKEN  COLUMN_SEP_TOKEN VARIABLE_TOKEN COLUMN_SEP_TOKEN IN_RANGE_TOKEN
SINGLE_BOUND_FOR_LOOP ::= FOR_VAR_IN_RANGE COLUMN_SEP_TOKEN KEYWORD_ARG
DOUBLE_BOUND_FOR_LOOP ::= FOR_VAR_IN_RANGE COLUMN_SEP_TOKEN KEYWORD_ARG COLUMN_SEP_TOKEN KEYWORD_ARG
DOUBLE_BOUND_FOR_LOOP_WITH_STEP ::= FOR_VAR_IN_RANGE COLUMN_SEP_TOKEN KEYWORD_ARG COLUMN_SEP_TOKEN KEYWORD_ARG COLUMN_SEP_TOKEN KEYWORD_ARG

FOR_LOOP_IN_RANGE ::= COLUMN_SEP_TOKEN (SINGLE_BOUND_FOR_LOOP | DOUBLE_BOUND_FOR_LOOP | DOUBLE_BOUND_FOR_LOOP_WITH_STEP) END_OF_LINE NEWLINE_TOKEN
                      INDENTED_KEYWORD_LINE+

FOR_VAR_IN ::= FOR_LOOP_START_TOKEN  COLUMN_SEP_TOKEN SINGLE_VARIABLE_LIST IN_TOKEN
FOR_LOOP_VALUE_LIST ::= (KEYWORD_ARG_LIST | INDENTED_ELLIPSES_FOR_TEST_TABLE)+
FOR_LOOP_IN ::= COLUMN_SEP_TOKEN  FOR_VAR_IN  FOR_LOOP_VALUE_LIST


KEYWORD ::= ROBOT_KEYWORD_TOKEN { methods=[getName setName getNameIdentifier getReference getReferences handleElementRename]}

KEYWORD_DEF ::= ROBOT_KEYWORD_DEF_TOKEN { methods=[getName setName getNameIdentifier getPresentation toString] }

ERROR ::= BAD_SYNTAX_TOKEN
