
{
  parserClass="com.jivesoftware.robot.intellij.plugin.parser.RobotParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Robot"
  psiImplClassSuffix="Impl"
  psiPackage="com.jivesoftware.robot.intellij.plugin.psi"
  psiImplPackage="com.jivesoftware.robot.intellij.plugin.psi.impl"

  elementTypeHolderClass="com.jivesoftware.robot.intellij.plugin.parser.RobotTypes"
  elementTypeClass="com.jivesoftware.robot.intellij.plugin.parser.RobotElementType"
  tokenTypeClass="com.jivesoftware.robot.intellij.plugin.lexer.RobotToken"

  psiImplUtilClass="com.jivesoftware.robot.intellij.plugin.elements.RobotImplUtil"
}

robotFile ::= (ROBOT_TABLE)*

ROBOT_TABLE ::= (SETTINGS_TABLE|VARIABLES_TABLE|TEST_CASES_TABLE|KEYWORDS_TABLE)

/* Settings Table Grammar */
SETTINGS_TABLE ::= SETTINGS_TABLE_HEADING SETTINGS_LINE*

SETTINGS_TABLE_HEADING ::= SETTINGS_TABLE_HEADING_TOKEN NEWLINE_TOKEN

SETTINGS_LINE ::= SETTING SETTING_ARG* NEWLINE_TOKEN

SETTING ::= ROBOT_KEYWORD_TOKEN
SETTING_ARG ::= ROBOT_KEYWORD_ARG_TOKEN

/* Variables table */
VARIABLES_TABLE ::= VARIABLES_TABLE_HEADING VARIABLES_LINE*

VARIABLES_TABLE_HEADING ::= VARIABLES_TABLE_HEADING_TOKEN NEWLINE_TOKEN

VARIABLES_LINE ::= ASSIGNMENT_TOKEN  (ROBOT_KEYWORD_ARG_TOKEN)+  NEWLINE_TOKEN

/* Test cases table */

TEST_CASES_TABLE ::= TEST_CASES_TABLE_HEADING TEST_CASE*

TEST_CASES_TABLE_HEADING ::= TEST_CASES_TABLE_HEADING_TOKEN NEWLINE_TOKEN

TEST_CASE ::= TEST_CASE_HEADER TEST_CASE_SETTINGS TESTCASE_LINE*

TEST_CASE_HEADER ::= TEST_CASE_HEADER_TOKEN  NEWLINE_TOKEN

TEST_CASE_SETTINGS ::= TEST_SETTING_LINE*

TEST_SETTING_LINE ::= TEST_SETTING  NEWLINE_TOKEN

TEST_SETTING ::= (DOCUMENTATION_SETTING|TAGS_SETTING|SETUP_SETTING|TEARDOWN_SETTING|TEMPLATE_SETTING|TIMEOUT_SETTING)

DOCUMENTATION_SETTING ::= DOCUMENTATION_SETTING_TOKEN   DOCUMENTATION_TOKEN
TAGS_SETTING ::= TAGS_SETTING_TOKEN  (TAG)*
SETUP_SETTING ::= SETUP_SETTING_TOKEN   KEYWORD_INVOCATION
TEARDOWN_SETTING ::= TEARDOWN_SETTING_TOKEN  KEYWORD_INVOCATION
TEMPLATE_SETTING ::= TEMPLATE_SETTING_TOKEN  ROBOT_KEYWORD_TOKEN
TIMEOUT_SETTING ::= TIMEOUT_SETTING_TOKEN TIMEOUT_VALUE_TOKEN

/* Keywords table */
KEYWORDS_TABLE ::= KEYWORDS_TABLE_HEADING KEYWORD_DEFINITION*

KEYWORDS_TABLE_HEADING ::= KEYWORDS_TABLE_HEADING_TOKEN  NEWLINE_TOKEN

KEYWORD_DEFINITION ::= KEYWORD_DEFINITION_HEADER KEYWORD_SETTINGS KEYWORD_LINE* RETURN_LINE?

KEYWORD_DEFINITION_HEADER ::= KEYWORD_DEF NEWLINE_TOKEN

KEYWORD_SETTINGS ::= KEYWORD_SETTING_LINE*
KEYWORD_SETTING_LINE ::= KEYWORD_SETTING NEWLINE_TOKEN
KEYWORD_SETTING ::= ARGUMENTS_SETTING | DOCUMENTATION_SETTING | TEARDOWN_SETTING | TIMEOUT_SETTING

ARGUMENTS_SETTING ::= ARGUMENTS_SETTING_TOKEN ARGUMENT_DEF*
ARGUMENT_DEF ::= VARIABLE_TOKEN | ASSIGNMENT_TOKEN KEYWORD_ARG

KEYWORD_LINE ::= ( KEYWORD_INVOCATION | VARIABLE_ASSIGN_TO_KEYWORD ) NEWLINE_TOKEN

RETURN_LINE ::= RETURN_SETTING NEWLINE_TOKEN
RETURN_SETTING ::= RETURN_SETTING_TOKEN  ROBOT_KEYWORD_ARG_TOKEN+


/* Shared grammar */
KEYWORD_INVOCATION ::= KEYWORD (KEYWORD_ARG)*
VARIABLE_ASSIGN_TO_KEYWORD ::= ASSIGNMENT_TOKEN  KEYWORD_INVOCATION
KEYWORD_ARG ::= VARIABLE_TOKEN | NUMBER_LITERAL_TOKEN | ROBOT_KEYWORD_ARG_TOKEN
TAG ::= TAG_TOKEN

KEYWORD ::= ROBOT_KEYWORD_TOKEN {mixin="com.jivesoftware.robot.intellij.plugin.elements.references.RobotNamedElementImpl"
                                          implements="com.jivesoftware.robot.intellij.plugin.elements.references.RobotNamedElement"
                                          methods=[getName setName getNameIdentifier getReference getReferences]}


KEYWORD_DEF ::= ROBOT_KEYWORD_DEF_TOKEN {mixin="com.jivesoftware.robot.intellij.plugin.elements.references.RobotNamedElementImpl"
                                                  implements="com.jivesoftware.robot.intellij.plugin.elements.references.RobotNamedElement"
                                                  methods=[getName setName getNameIdentifier getPresentation]}

ERROR ::= BAD_CHAR_TOKEN

OTHER ::= COLUMN_SEP_TOKEN | SIGNLE_SPACE_TOKEN | TABLE_HEADING_TOKEN